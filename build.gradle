plugins {
    id 'war'
    id 'java'
    id "com.github.spotbugs" version "2.0.0"
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
}

group 'net.novucs'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.derby', name: 'derbyclient', version: '10.14.2.0'
    providedCompile group: 'javax', name: 'javaee-api', version: '8.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.apache.derby', name: 'derby', version: '10.14.2.0'
    testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
}

task autodeploy(type: Copy) {
    from war.archiveFile
    into "build"
    rename "esd-${project.version}.war", "app.war"
}

spotbugs {
    toolVersion = '4.0.0-beta4'
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion '8.24'
    configFile file('config/checkstyle/checkstyle.xml');
}

pmd {
    toolVersion '6.18.0'
    ruleSets = []
    ruleSetFiles = files("config/pmd/excludes.xml")
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

task checkBuildReport {
    dependsOn 'check'
    dependsOn 'jacocoTestReport'
    dependsOn 'jacocoTestCoverageVerification'
    dependsOn 'autodeploy'
    tasks.findByName('jacocoTestReport').mustRunAfter 'check'
    tasks.findByName('autodeploy').mustRunAfter 'jacocoTestReport'
}
